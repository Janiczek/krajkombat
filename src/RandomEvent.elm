module RandomEvent exposing (RandomEvent, listGenerator)

import Random exposing (Generator)
import ResourceDelta exposing (ResourceDelta(..), add, sub)


type alias RandomEvent =
    { flavorText : String
    , deltas : List ResourceDelta
    , isGood : Bool
    }


{-| TODO use ranges instead of constants
-}
goodGenerator : Generator RandomEvent
goodGenerator =
    Random.uniform ( "Dotace vy코ly hej!", [ add 150 300 AP ] )
        [ ( "Na코 tym ty Pra쬬ky zas upln캩 rozdupal!", [ add 40 80 AP, add 15 30 BBV ] )
        , ( "Hacke콏i z SPE se n캩kam naburali...", [ add 140 200 AP, add 1 2 BREF ] )
        , ( "Hacke콏i z SPE zjistili 쬰 databaze KrajKombatu ma defaultni heslo 游", [ add 10 20 BBV ] )
        , ( "Na코e 코kola byla nejlep코i v republice kamo", [ add 40 80 AP, add 20 40 APPerMonth, add 3 5 GREF ] )
        ]
        |> ResourceDelta.bundleGenerator
        |> Random.map
            (\( flavorText, deltas ) ->
                { flavorText = flavorText
                , deltas = deltas
                , isGood = True
                }
            )


{-| TODO use ranges instead of constants
-}
badGenerator : Generator RandomEvent
badGenerator =
    Random.uniform ( "P콏i코li nam na podvod.", [ sub 40 70 AP, add 2 5 BREF ] )
        [ ( "Na tom h콏i코ti sme to moc nedali...", [ add 5 10 BREF, sub 1 2 GREF ] )
        , ( "Zas n캩jaky doping 游눌", [ sub 20 40 AP, sub 2 4 BBVPerMonth ] )
        , ( "Hacke콏i z SPE po sob캩 nezametli stopy kuva", [ sub 30 60 AP ] )
        ]
        |> ResourceDelta.bundleGenerator
        |> Random.map
            (\( flavorText, deltas ) ->
                { flavorText = flavorText
                , deltas = deltas
                , isGood = False
                }
            )


listGenerator : { resources | gref : Int, bref : Int } -> Generator (List RandomEvent)
listGenerator { gref, bref } =
    Random.map2 (++)
        (listGenerator_ gref goodGenerator)
        (listGenerator_ bref badGenerator)


listGenerator_ : Int -> Generator RandomEvent -> Generator (List RandomEvent)
listGenerator_ ref gen =
    let
        sureEvents : Int
        sureEvents =
            ref // 100

        bonusPercent : Int
        bonusPercent =
            ref - sureEvents * 100
    in
    Random.int 0 100
        |> Random.andThen
            (\percentage ->
                let
                    bonus : Int
                    bonus =
                        if percentage < bonusPercent then
                            1

                        else
                            0

                    count : Int
                    count =
                        sureEvents + bonus
                in
                Random.list count gen
            )
